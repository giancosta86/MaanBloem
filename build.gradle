/*ยง
  ===========================================================================
  MaanBloem
  ===========================================================================
  Copyright (C) 2016 Gianluca Costa
  ===========================================================================
  Licensed under the Apache License, Version 2.0 (the "License");
  you may not use this file except in compliance with the License.
  You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

  Unless required by applicable law or agreed to in writing, software
  distributed under the License is distributed on an "AS IS" BASIS,
  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  See the License for the specific language governing permissions and
  limitations under the License.
  ===========================================================================
*/

buildscript {
    repositories {
        maven {
            url 'https://dl.bintray.com/giancosta86/Hephaestus'
        }

        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1'
        classpath 'info.gianlucacosta.aurora:aurora:2.1'
        classpath 'info.gianlucacosta.moonlicense:moonlicense-gradle:4.0'
        classpath 'info.gianlucacosta.moondeploy:moondeploy-gradle:3.1'
    }
}


apply plugin: 'scala'
apply plugin: 'info.gianlucacosta.aurora'
apply plugin: 'info.gianlucacosta.moondeploy'
apply plugin: 'application'


group 'info.gianlucacosta.maanbloem'
archivesBaseName = 'maanbloem'
version '1.1'


description = 'App manager for MoonDeploy'


mainClassName = "info.gianlucacosta.maanbloem.App"


repositories {
    mavenCentral()
}

dependencies {
    compile 'org.scala-lang:scala-library:2.11.8'
    compile 'org.scalafx:scalafx_2.11:8.0.60-R9'

    compile 'javax.json:javax.json-api:1.0'
    compile 'org.glassfish:javax.json:1.0.4'

    compile 'org.jclarion:image4j:0.7'
}


startScripts {
    def originalWindowsTemplateString = windowsStartScriptGenerator.template.asString()
    def javawTemplateString =
            originalWindowsTemplateString
                    .replace("java.exe", "javaw.exe")
                    .replace("\"%JAVA_EXE%\" %DEFAULT_JVM_OPTS%", "start \"\" /B \"%JAVA_EXE%\" %DEFAULT_JVM_OPTS%")
    def javawTemplate = project.resources.text.fromString(javawTemplateString)
    windowsStartScriptGenerator.template = javawTemplate
}


aurora {
    docTask = 'scaladoc'
    gitHubUser = 'giancosta86'

    author {
        name = 'Gianluca Costa'
        email = 'gianluca@gianlucacosta.info'
        url = 'http://gianlucacosta.info/'
    }

    bintray {
        user = project.bintrayUser
        key = project.bintrayKey
        repo = 'Hephaestus'
        licenses = ['Apache-2.0']
        labels = ['MoonDeploy', 'app', 'manager']
    }
}


moonLicense {
    license = apache2

    productInfo {
        productName = 'MaanBloem'
        inceptionYear = 2016
        copyrightHolder = 'Gianluca Costa'
    }
}



task createAppDescriptor {
    dependsOn(distZip)
} << {
    ant.moonDeploy(
            baseURL: "${project.url}/releases/latest",

            name: "${moonLicense.productInfo.productName}",
            version: project.version,

            description: project.description,
            publisher: "Gianluca Costa",

            skipPackageLevels: 1,

            iconPath: "mainIcon.png"
    ) {
        commandLine {
            param("bash")
            param("bin/${project.name}")
        }

        pkg(name: "${project.name}-${project.version}.zip")

        os(
                name: "windows",
                iconPath: "mainIcon.ico"
        ) {
            commandLine {
                param("bin\\${project.name}.bat")
            }
        }
    }
}

tasks["assemble"].dependsOn("createAppDescriptor")